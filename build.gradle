plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'maven'
}

jacoco {
    toolVersion = "0.8.5"
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
// Do not change this version number
version buildNumber

sourceCompatibility = 11
targetCompatibility = 11

repositories {

    mavenLocal()

    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }

    jcenter()
    mavenCentral()

    // jitpack should be last resort
    // see: https://github.com/jitpack/jitpack.io/issues/1939
    maven { url 'https://jitpack.io' }
}

// region static code analysis

checkstyle {
    maxWarnings = 0
    toolVersion = '8.10.1'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.19.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-pdf-email-common'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'sscs-pdf-email-common'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/sscs-pdf-email-common'
        version {
            name = project.version
        }
    }
}

def versions = [
    reformLogging: '5.0.1'
]

dependencies {
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: versions.reformLogging
    compile group: 'uk.gov.hmcts.reform', name: 'pdf-service-client', version: '7.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'sscs-common', version: '3.4.52'
    compile group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '7.0.0'

    compile group: 'org.springframework', name: 'spring-context-support', version: '5.2.8.RELEASE'
    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    compile group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.12.1'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'

    compile group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.24.1'
    compile group: 'org.apache.tika', name: 'tika-parsers', version: '1.24.1'
    compile group: 'net.coobird', name: 'thumbnailator', version: '0.4.12'
    compile group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff', version: '3.6'
    compile group: 'org.apache.pdfbox', name: 'xmpbox', version: '2.0.21'
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.20'
    compile group: 'pl.touk', name: 'throwing-function', version: '1.3'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.8.1'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor "org.projectlombok:lombok:1.18.12"

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor "org.projectlombok:lombok:1.18.12"
    testCompile group: 'junit', name: 'junit', version: 4.12
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.5.2'
    testCompile 'pl.pragmatists:JUnitParams:1.1.1'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'

}
