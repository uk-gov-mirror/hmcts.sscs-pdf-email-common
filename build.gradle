plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

def buildNumber = System.getenv("TRAVIS_BUILD_NUMBER") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version '0.0.' + buildNumber

sourceCompatibility = 1.8

repositories {

    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }

    jcenter()
    mavenCentral()

    // jitpack should be last resort
    // see: https://github.com/jitpack/jitpack.io/issues/1939
    maven { url 'https://jitpack.io' }
}

// region static code analysis

checkstyle {
    maxWarnings = 0
    toolVersion = '8.10.1'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.5.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// endregion

// region test coverage

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

// endregion

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-pdf-robotics-email-common'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'sscs-pdf-robotics-email-common'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/sscs-pdf-robotics-email-common'
        version {
            name = project.version
        }
    }
}

dependencies {
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '3.0.1'
    compile group: 'uk.gov.hmcts.reform', name: 'sscs-common', version: '0.0.60'
    compile group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '3.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'pdf-service-client', version: '5.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'sscs-common', version: '0.0.74'

    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
    compile group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.9.2'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'

    compile group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'

    compileOnly 'org.projectlombok:lombok:1.16.20'

    testCompile group: 'junit', name: 'junit', version: 4.12
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.7'

}
